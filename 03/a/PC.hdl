// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Register(in=regIn, load=true, out=regOut, out=out);
    Add16(a=regOut, b[0]=true, b[1..15]=false, out=incremented);

    Mux8Way16(
      a=regOut,     // 000 else
      b=false,      // 001 reset
      c=in,         // 010 load
      d=false,      // 011 reset
      e=incremented,// 100 inc
      f=false,      // 101 reset
      g=in,         // 110 load
      h=false,      // 111 reset
      sel[0]=reset,
      sel[1]=load,
      sel[2]=inc,
      out=regIn);
}
